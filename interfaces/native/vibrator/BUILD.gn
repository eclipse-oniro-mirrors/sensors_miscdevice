# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("./../../../miscdevice.gni")

ohos_ndk_library("libvibrator_ndk") {
  output_name = "vibrator_agent"
  ndk_description_file = "./libvibrator.json"
  min_compact_version = "6"
}

ohos_ndk_headers("vibrator_ndk_header") {
  dest_dir = "$ndk_headers_out_dir/sensors"
  sources = [
    "include/vibrator_agent.h",
    "include/vibrator_agent_type.h",
  ]
}

config("vibrator_config") {
  include_dirs = [ "include" ]
}

ohos_shared_library("vibrator_interface_native") {
  output_name = "vibrator_agent"
  sources = [ "src/vibrator_agent.cpp" ]

  include_dirs = [
    "$SUBSYSTEM_DIR/frameworks/native/miscdevice/include",
    "$SUBSYSTEM_DIR/interfaces/native/light/include",
    "$SUBSYSTEM_DIR/interfaces/native/vibrator/include",
    "$SUBSYSTEM_DIR/utils/include",
  ]

  if (miscdevice_feature_vibrator_custom) {
    defines = [ "OHOS_BUILD_ENABLE_VIBRATOR_CUSTOM" ]
  }

  cflags = [ "-Wno-error=inconsistent-missing-override" ]

  deps = [
    "$SUBSYSTEM_DIR/frameworks/native/miscdevice:libvibrator_native",
    "$SUBSYSTEM_DIR/interfaces/native/light:light_ndk_header",
    "$SUBSYSTEM_DIR/interfaces/native/vibrator:libvibrator_ndk",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
  ]

  public_configs = [ ":vibrator_config" ]
  innerapi_tags = [ "platformsdk" ]
  part_name = "miscdevice"
  subsystem_name = "sensors"
}

group("vibrator_target") {
  deps = [ ":vibrator_interface_native" ]
}
